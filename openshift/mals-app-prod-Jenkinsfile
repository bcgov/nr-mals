APP_NAME = "mals-app"
PROJECT_NAMESPACE = "30245e"
ENV_LABEL="prod"

class AppEnvironment {
  String name
  String project
  String tag
  String previousTag
}

// EDIT LINE BELOW (Edit your environment TAG names)
environments = [
  dev:new AppEnvironment(name:"Development",project:"dev",tag:"dev",previousTag:"dev-previous"),
  uat:new AppEnvironment(name:"UAT",project:"test",tag:"uat",previousTag:"uat-previous"),
  test:new AppEnvironment(name:"Test",project:"test",tag:"test",previousTag:"test-previous"),
  prod:new AppEnvironment(name:"Prod",project:"prod",tag:"prod",previousTag:"prod-previous")
]

// You shouldn't have to edit these if you're following the conventions
ARTIFACT_BUILD = "${APP_NAME}-${ENV_LABEL}"

IMAGESTREAM_NAME = "${APP_NAME}"

PATHFINDER_URL = "pathfinder.gov.bc.ca"


// Gets the container hash for the latest image in an image stream
def getLatestHash(imageStreamName) {
  return sh (
    script: """oc get istag ${imageStreamName}:latest-${ENV_LABEL} -o=jsonpath='{@.image.metadata.name}' | sed -e 's/sha256://g'""",
    returnStdout: true
  ).trim()
}

// Gets all tags already applied to this ImageStream (as a single string); e.g., 'dev test dev-previous my-other-tag ...'
def getAllTags(imageStreamName) {
  return sh (
    script: """oc get is ${imageStreamName} -o template --template='{{range .status.tags}}{{" "}}{{.tag}}{{end}}'""",
    returnStdout: true
  ).trim()
}

// Checks whether we are running this pipeline for the first time by looking at what tags are available on the application's ImageStream
def tagExists(tagName, imageStream) {
  def tags = getAllTags(imageStream)
  def entries = tags.split(" ")
  for (entry in entries) {
    if (entry == tagName) {
      return true
    }
  }
  return false
}

def buildAndVerify(buildConfigName) {
  echo "Building: ${buildConfigName}"
  openshiftBuild(
    bldCfg: buildConfigName,
    showBuildLogs: 'true',
    waitTime: '900000'
  )
  openshiftVerifyBuild(
    bldCfg: buildConfigName,
    showBuildLogs: 'true',
    waitTime: '900000'
  )
}

def tagImage(srcHash, destination, imageStream) {
  openshiftTag(
    destStream: imageStream,
    verbose: 'true',
    destTag: destination,
    srcStream: imageStream,
    srcTag: srcHash,
    waitTime: '900000'
  )
}

// Keeps a copy of last good known configuration for a deployment (just in case)
def tagLatestStable(tag, backupTag, imageStream) {
  // skip this on the first run... there's nothing to backup!
  if (tagExists(tag, imageStream)) {
    tagImage(tag, backupTag, imageStream)
  }
}

def deployAndVerify(srcHash, project, tag, imageStream) {
  echo "Deploying ${APP_NAME} to ${project}"
  tagImage(srcHash, tag, imageStream)
  // verify deployment to a project; e.g. [your-project-name]-dev
  openshiftVerifyDeployment(
    deploymentConfig: ARTIFACT_BUILD,
    namespace: "${PROJECT_NAMESPACE}-${project}",
    waitTime: '900000'
  )
}

// Generates a string representation of the current code changes that triggered a build
def getChangeString() {
  def MAX_MSG_LEN = 512
  def changeString = ""
  def changeLogSets = currentBuild.changeSets
  for (int i = 0; i < changeLogSets.size(); i++) {
     def entries = changeLogSets[i].items
     for (int j = 0; j < entries.length; j++) {
         def entry = entries[j]
         truncated_msg = entry.msg.take(MAX_MSG_LEN)
         changeString += " - ${truncated_msg} [${entry.author}]\n"
     }
  }
  if (!changeString) {
     changeString = "No changes"
  }
  return changeString
}

def notifyGood(title, description) {
  // TODO: Send notifications to Slack
  echo title
  if (description) {
    echo description
  }
}

def notifyError(title, description) {
  // TODO: Send notifications to Slack
  echo title
  if (description) {
    echo description
  }
}

node('master') {
  stage('Startup') {
    // stop pending builds.
    sh "oc cancel-build bc/${ARTIFACT_BUILD}"
  }
  
  stage('Build') {
    echo "Building Application image..."
    buildAndVerify(ARTIFACT_BUILD)
  
    IMAGE_HASH = getLatestHash(IMAGESTREAM_NAME)
    echo ">> IMAGE_HASH: ${IMAGE_HASH}"  
  }

  stage('Deploy on PROD') {
    def environment = environments.prod
    def tag = environment.tag
    def project = environment.project
    def stableTag = environment.previousTag
    node {
      try {
        // hold on to a copy of the last stable environment (in case the upcoming deployment fails...)
        tagLatestStable(tag, stableTag, IMAGESTREAM_NAME)
        deployAndVerify(IMAGE_HASH, project, tag, IMAGESTREAM_NAME)
        // all is good!
        notifyGood("New ${APP_NAME} in ${project} :)", "Changes: ${getChangeString()}")
      } catch(error) {
        notifyError("Couldn't deploy ${APP_NAME} to ${project} :(", "Error: '${error.message}'")
        throw error
      }
    }
  }
}
